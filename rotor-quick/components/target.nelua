require 'nene.math'
require 'rotor-quick.makers'

global RotorQuick.Components.Target = @record{
  pos_target_id: GenerationalIndex,
}
local Target = RotorQuick.Components.Target
## impl_is_component(Target)

function Target:get_target_pos(): (*RotorQuick.Components.Position, boolean, string)
  local target_pos_opt = RotorQuick.Components.Position.get_entry_from_id(self.pos_target_id)
  if not target_pos_opt.filled then
    return nilptr, false, 'position from pos_target_id not found'
  end

  return target_pos_opt:get(), true, ''
end

function Target.get_direction_from_pos(self_position: Nene.Math.Vec2, target_pos: Nene.Math.Vec2): Nene.Math.Vec2
  local dir = Nene.Math.Vec2.normalized(target_pos - self_position)
  return dir
end

function Target:get_direction(self_position: Nene.Math.Vec2): (Nene.Math.Vec2, boolean, string)
  local target_pos, ok, errmsg = self:get_target_pos()
  if not ok then
    return (@Nene.Math.Vec2)(), false, errmsg
  end
  local target_gl_pos = target_pos:get_global_position()

  return Target.get_direction_from_pos(self_position, target_gl_pos), true, ''
end

function Target:get_distance(self_position: Nene.Math.Vec2): (number, boolean, string)
  local target_pos, ok, errmsg = self:get_target_pos()
  if not ok then
    return 0, false, errmsg
  end
  local target_gl_pos = target_pos:get_global_position()

  local diff = target_gl_pos - self_position
  return #diff, true, ''
end
