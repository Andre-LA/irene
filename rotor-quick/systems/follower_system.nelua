require 'math'
require 'raylib'
require 'rotor'
require 'rotor-quick.makers'
require 'rotor-quick.components.position'
require 'rotor-quick.components.follower'

local Position = RotorQuick.Components.Position
local Follower = RotorQuick.Components.Follower

global RotorQuick.Systems.FollowerSystem = @MakeSystem('FollowerSystem', @record{
  follower: Follower,
  position: Position,
})

local FollowerSystem = RotorQuick.Systems.FollowerSystem

function FollowerSystem:run()
  ## if PROF then
    nprof:push(#[FollowerSystem.value.nickname]#)
    defer nprof:pop() end
  ## end

  for _, components in self:iterate_components() do
    local follower: *Follower = components.follower
    local position: *Position = components.position

    local followed_position_opt = follower:get_position()
    if followed_position_opt.filled then
      local followed_position: *Position = followed_position_opt:get()
      position:set_local_from_other_origin(followed_position.origin_position)
    end
  end
end
