require 'rotor-quick.makers'
require 'rotor-quick.components.velocity'
require 'rotor-quick.components.position'

local RQ_Comps = RotorQuick.Components

global RotorQuick.Systems.VelocityApplier = @MakeSystem('VelocityApplier', @record{
  velocity: RQ_Comps.Velocity,
  position: RQ_Comps.Position,
})

local VelocityApplier = RotorQuick.Systems.VelocityApplier

function VelocityApplier:run(dt: number)
  ## if PROF then
    nprof:push(#[VelocityApplier.value.nickname]#)
    defer nprof:pop() end
  ## end

  for _, components in self:iterate_components() do
    local velocity = components.velocity
    local position = components.position

    if not velocity.active then
      continue
    end

    if velocity.velocity_is_limited then
      velocity.final_velocity = RQ_Comps.Velocity.get_limited_velocity(velocity.final_velocity, velocity.max_velocity)
    end

    position.local_position.x = position.local_position.x + velocity.final_velocity.x * dt
    position.local_position.y = position.local_position.y + velocity.final_velocity.y * dt
  end
end

