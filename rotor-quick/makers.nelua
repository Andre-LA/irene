## local tabler = require 'nelua.utils.tabler'
## local metalib = require 'rotor-quick.metalib'

require 'rotor-quick.core'

## if PROF then
  require 'nprof'
  global nprof: NProf = {}
## end

##[[
  local function is_record(_type)
    return metalib.basic_typecheck(_type, 'record', function(value)
      if _type and _type.value and traits.is_type(_type.value) and _type.value.is_record then
        return true
      else
        tostring(_type)
      end
    end)
  end

  local function gen_errmsg(comp_type, err_msg)
    local comp_name = comp_type.name, metalib.pascalcase_to_snakecase(comp_type.name)
    return string.format(
      'assertion error with type %s: %s. Maybe you forgot to use ## is_component(%s)?',
      comp_name,
      err_msg,
      comp_name
    )
  end

  local function assert_type_is_component(comp_type)
    staticassert(is_record(comp_type))

    local c_type, err_msg = metalib.basic_typecheck(comp_type.value, "record with 'is_component' trait", function(value)
      local is_type = traits.is_type(value)
      local is_record = is_type and value.is_record
      local is_component = is_record and value.is_component

      if is_component then
        return true
      elseif is_record then
        return "record without 'is_component' trait"
      else
        return tostring(value)
      end
    end)

    staticassert(c_type, gen_errmsg(comp_type, err_msg)) -- does assertion failure when ## is_component wasn't used

    return comp_type
  end
]]

-- [[ mark record as component [[

-- this macro auto-implements the is_component trait
## function impl_is_component(R, use_self_ids)
  -- ensure that a record is passed
  ## staticassert(is_record(R))

  -- mark the is_component trait
  ## local R_snakecased_name = metalib.pascalcase_to_snakecase(R.name)

  -- mark the is_component trait
  ## R.value.is_component = true
  -- mark component-specific trait
  ## R.value["is_"..R_snakecased_name] = true
  -- add a `component_metainfo` table
  ## R.value.component_metainfo = {snakecase_name = R_snakecased_name, use_self_ids = use_self_ids}
  -- macro do add the mask and the storage of this component
  ## RQ_add_mask_and_storage(R_snakecased_name, R)

  -- if use_self_ids is true, add self_id and entity_id fields, of type GenerationalIndex
  ##[[
  if use_self_ids then
    R.value:add_field('self_id', GenerationalIndex.value)
    R.value:add_field('entity_id', GenerationalIndex.value)
  end
  ]]

  local R: type = #[R]#

  function R.get_entry_from_id(id: GenerationalIndex): optional(*R)
    return RotorQuick.storages.#|R_snakecased_name|#:get_entry(id)
  end

  function R.get_entry_from_entity_id(id: GenerationalIndex): optional(*R)
    local result: optional(*R) = {}

    local entity_opt = RotorQuick.storages.entity:get_entry(id)

    if entity_opt.filled then
      local entity = entity_opt:get()
      local r_storage_index_opt = entity:get_storage_index(RotorQuick.masks.#|R_snakecased_name|#)
      if r_storage_index_opt.filled then
        local r_storage_index = r_storage_index_opt:get()

        local entity_r_id = entity.associated_components[r_storage_index]

        local entity_r_opt = R.get_entry_from_id(entity_r_id)
        if entity_r_opt.filled then
          result:set(entity_r_opt:get())
        end
      end
    end

    return result
  end
## end

-- temporary alias
## is_component = impl_is_component

-- ]] mark record as component ]]

-- [[ Entity maker [[
## local n = {'st', 'nd', 'rd'}
## local function nth(i) return i < 4 and i..n[i] or i..'th' end

## local make_entity = generalize(function(entity_name, ...)
  ##[[
    -- TODO: also should asserts if the first character is a number
    staticassert(not string.find(entity_name, '[^_%w]'), 'entity name must contains only alphanumeric and/or underscores')

    local entity_shape = {...}
    local components, children = {}, {}

    local entity_subnickname = table.concat(
      tabler.imap(entity_shape, function(v) return v.nickname end),
      ', '
    )
  ]]

  local EntityT <nickname #['entity('..entity_subnickname..')']#, codename #['rotor_quick_'..entity_name]#> = @record{
    entity_id: GenerationalIndex,
    -- more added below
  }

  ## EntityT.value.is_entity = true
  ## EntityT.value.entity_metainfo = {name = entity_name}

  function EntityT:get_entity(): *Entity
    local entity_opt = RotorQuick.storages.entity:get_entry(self.entity_id)
    check(
      entity_opt.filled,
      #["Could not get " .. entity_name .. "'s entity"]#
    )
    return entity_opt:get()
  end

  local EntityInitValues = @record{--[[ more added below ]]}

  ##[[
    -- collect entities and components
    for i = 1, #entity_shape do
      -- ensures that there are only components or entities in entity_shape
      local msg_nth = 'error on '..nth(i)..' parameter: '
      staticassert(entity_shape[i].is_component or entity_shape[i].is_entity, msg_nth..'is neither component or entity')

      if entity_shape[i].is_component then
        table.insert(components, entity_shape[i])
      elseif entity_shape[i].is_entity then
        table.insert(children, entity_shape[i])
      end
    end
  ]]

  -- add fields and functions to EntityT for each component
  -- it also configures EntityInitValues
  ## for i, comp in ipairs(components) do
    ## local comp_scname = comp.component_metainfo.snakecase_name

    -- add the corresponding id field
    ## EntityT.value:add_field(comp_scname..'_id', GenerationalIndex.value)

    -- add the init
    ## EntityInitValues.value:add_field(comp_scname, comp)

    -- add EntityT:get_"component" method
    function EntityT:#|'get_'..comp_scname|#(): pointer(#[comp]#)
      local possible_result = RotorQuick.storages.#|comp_scname|#:get_entry(self.#|comp_scname..'_id'|#)

      check(
        possible_result.filled,
        #[EntityT.value.nickname..':get_'..comp_scname..': '..comp_scname..' is not associated with entity']#
      )

      return possible_result:get()
    end
  ## end

  -- add fields and functions to EntityT for each child entity
  -- add the child type to EntityInitValues
  ## for i, child in ipairs(children) do
    ## local child_name = child.entity_metainfo.name
    ## EntityT.value:add_field(child_name, child)

    local ChildType: type = #[EntityT.value.fields[child_name].type]#
    ## EntityInitValues.value:add_field(child_name, ChildType.value)

    function EntityT:#|'get_'..child_name|#(): (ChildType, *Entity)
      local child_value = self.#|child_name|#
      local child_entity = child_value:get_entity()

      return child_value, child_entity
    end
  ## end

  function EntityT.new(initial_values: EntityInitValues): (EntityT, *Entity)
    local result: EntityT = {}

    local entity_id, entity = RotorQuick.storages.entity:new_entry(Entity.new())
    result.entity_id = entity_id

    -- create and associate each component
    ## for i, comp in ipairs(components) do
      ## local comp_scname = comp.component_metainfo.snakecase_name
      ## local c_id = comp_scname..'_id'

      local #|c_id|#, #|comp_scname|# = RotorQuick.storages.#|comp_scname|#:new_entry(initial_values.#|comp_scname|#)
      entity:associate(#|c_id|#, RotorQuick.masks.#|comp_scname|#)

      ## if comp.component_metainfo.use_self_ids then
        #|comp_scname|#.self_id = #|c_id|#
        #|comp_scname|#.entity_id = entity_id
      ## end

      result.#|c_id|# = #|c_id|#
    ## end

    -- children are already created, so it should be just copied
    ## for i, child in ipairs(children) do
      ## local child_name = child.entity_metainfo.name
      result.#|child_name|# = initial_values.#|child_name|#
    ## end

    return result, entity
  end

  ## return EntityT
## end)

global MakeEntity = #[make_entity]#

-- ]] Entity maker ]]

-- [[ system maker [[

## local ins = function(v) return require 'nelua.thirdparty.inspect' (v, {depth = 2}) end

## local make_system = generalize(function(nickname, run_T)
  ##[[
  local component_names = {}
    local component_types = {}

    for _, field in ipairs(run_T.fields) do
      local ftype = field.type
      staticassert(traits.is_type(ftype) and ftype.is_component, 'MakeSystem: type %s is not a component!', ftype)

      table.insert(component_names, field.name)
      table.insert(component_types, ftype)
    end
  ]]

  local optCollectionT = @record{}
  local ptrCollectionT = @record{}

  ## for i, name in ipairs(component_names) do
    local ptr_T = @pointer(#[ component_types[i] ]#)
    ## ptrCollectionT.value:add_field(name, ptr_T.value)

    local opt_T = @optional(ptr_T)
    ## optCollectionT.value:add_field(name, opt_T.value)

    local stor_T = @pointer(Storage( #[ component_types[i] ]# ))
  ## end

  local systemT <nickname #[string.format('system(%s)', nickname)]#> = @record{
    system_data: SystemData
  }

  function systemT.new(): systemT
    local components: vector(BitsetArray) = {}
    components:reserve(#[#component_names]#)

    local new_system: systemT = {}

    ## for _, name in ipairs(component_names) do
      components:push(BitsetArray.copy(RotorQuick.masks.#|name|#))
    ## end

    new_system.system_data = SystemData.new(components)
    return new_system
  end

  function systemT.components_are_available(opts: *optCollectionT): boolean
    local result: boolean = true

    ## for i, name in ipairs(component_names) do
      result = result and opts.#|name|#.filled
    ## end

    return result
  end

  function systemT:update(entity: *Entity)
    self.system_data:update(entity)
  end

  local IterState = @record{
    system_data: *SystemData,
    ids_iterator: function(ids_vec: *vector(vector(GenerationalIndex)), index: isize): (boolean, isize, vector(GenerationalIndex)),
    ids_vec: *vector(vector(GenerationalIndex)),
    index: isize
  }

  function systemT:iterate_components(): (
    function(state: *IterState, index: isize): (boolean, isize, ptrCollectionT),
    IterState,
    isize
  )
    local function iterator(state: *IterState, index: isize): (boolean, isize, ptrCollectionT)
      while state.index < #state.ids_vec do
        local can_iter, index, ids = state.ids_iterator(state.ids_vec, state.index)
        state.index = index

        if can_iter then
          local opts: optCollectionT = {}

          ## for i, field in ipairs(ptrCollectionT.value.fields) do
            ## local component_snakecase_name = field.type.subtype.component_metainfo.snakecase_name
            opts.#|field.name|# = RotorQuick.storages.#|component_snakecase_name|#:get_entry( ids[ #[i-1]# ] )
          ## end

          if systemT.components_are_available(opts) then
            local iter_result: ptrCollectionT = {}

            ## for _, field in ipairs(ptrCollectionT.value.fields) do
              iter_result.#|field.name|# = opts.#|field.name|#:get()
            ## end

            return true, index, iter_result
          else
            state.system_data:mark_available(state.index)
          end
        else
          return false, -1, (@ptrCollectionT){}
        end
      end

      return false, -1, (@ptrCollectionT){}
    end

    local ids_iterator, ids_vec, index = self.system_data:iterate_ids()
    return iterator, (@IterState){ system_data = &self.system_data, =ids_iterator, =ids_vec, =index }, -1
  end

  ## return systemT
## end)

global MakeSystem = #[make_system]#

-- ]] system maker ]]
