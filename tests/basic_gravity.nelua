-- minimal example of a box falling

##[[
  TICO = {
    L = 'tinycoffee/build',
    I = {
      include = "tinycoffee/include",
      external = "tinycoffee/external"
    }
  }
]]

require 'tico'
require 'rotor'
require 'rotor-quick'
require 'rotor-quick.makers'

local nelua_argc: cint <cimport, nodecl>
local nelua_argv: *[0]cstring <cimport, nodecl>

local Config = tico.Config

local Position = RotorQuick.Components.Position
local Shape = RotorQuick.Components.Shape
local Painter = RotorQuick.Components.Painter
local Velocity = RotorQuick.Components.Velocity
local Gravity = RotorQuick.Components.Gravity

local GravityApplier = RotorQuick.Systems.GravityApplier
local ShapePainter = RotorQuick.Systems.ShapePainter
local VelocityApplier = RotorQuick.Systems.VelocityApplier

local gravity_applier = GravityApplier.new()
local shape_painter = ShapePainter.new()
local velocity_applier = VelocityApplier.new()

local SCREEN_WIDTH: integer <comptime> = 800
local SCREEN_HEIGHT: integer <comptime> = 450

local FallingBox = @MakeEntity('falling_box',
  Position,
  Shape,
  Painter,
  Velocity,
  Gravity
)

local falling_box, falling_box_entity = FallingBox.new{
  position = {
    origin_position = {32, 32},
  },
  shape    = {
    category = Shape.ShapeCategory.Rectangle,
    vertices = { {0, 0}, {16, 0}, {16, 16}, {0, 16} }
  },
  painter  = {
    color = tico.WHITE,
    active = true
  },
  velocity = {
    active = true,
    max_velocity = 10,
  },
  gravity  = {
    acceleration = 0.1,
    direction = {0,1},
    active = true
  }
}

gravity_applier.system_data:update(falling_box_entity)
shape_painter.system_data:update(falling_box_entity)
velocity_applier.system_data:update(falling_box_entity)

local config = tico.config_init("Basic Gravity test", 800, 450, nelua_argc, nelua_argv)
tico.init(config)

while not tico.window_should_close() do
  tico.update()

  gravity_applier:run()
  velocity_applier:run()

  tico.begin_draw()
  do
    tico.graphics_clear(tico.BLACK)
    shape_painter:run()
  end
  tico.end_draw()
end

tico.terminate()
