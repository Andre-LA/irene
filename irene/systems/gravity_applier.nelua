require 'irene.makers'
require 'irene.components.position'
require 'irene.components.shape'
require 'irene.components.painter'

local Comps, Sys = @Irene.Comps, @Irene.Sys

local GravityApplier = @MakeSystem('GravityApplier', @record{
  gravity: Comps.Gravity,
  velocity: Comps.Velocity
})

global Sys.GravityApplier = @GravityApplier

function GravityApplier:run(dt: number)
  ## if PROF then
    nprof:push(#[GravityApplier.value.nickname]#)
    defer nprof:pop() end
  ## end

  for _, components in self:iterate_components() do
    local gravity = components.gravity
    local velocity = components.velocity

    if not gravity.active then
      continue
    end

    velocity:add_velocity(gravity.direction * gravity.acceleration * dt)
  end
end

