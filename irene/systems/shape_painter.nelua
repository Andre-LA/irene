require 'math'
require 'nene'
require 'rotor'
require 'irene.makers'
require 'irene.components.position'
require 'irene.components.shape'
require 'irene.components.painter'

local Rectf = @Math.Rectf
local Comps, Sys = @Irene.Comps, @Irene.Sys

local ShapePainter = @MakeSystem('ShapePainter', @record{
  position: Comps.Position,
  shape: Comps.Shape,
  painter: Comps.Painter
})

global Sys.ShapePainter = @ShapePainter

function ShapePainter:run(nene: *Nene)
  ## if PROF then
    nprof:push(#[ShapePainter.value.nickname]#)
    defer nprof:pop() end
  ## end

  for _, components in self:iterate_components() do
    local position = components.position
    local shape    = components.shape
    local painter  = components.painter

    if not painter.active then
      continue
    end

    local size_x, size_y = 0.0, 0.0

    for i = 0, < #shape.vertices do
      size_x = math.max(shape.vertices[i].x, size_x)
      size_y = math.max(shape.vertices[i].y, size_y)
    end

    local global_pos = position:get_global_position()

    local ShapeCategory = @Irene.Comps.Shape.ShapeCategory

    switch shape.category
    case ShapeCategory.Rectangle then
      local rectf: Rectf = { global_pos.x, global_pos.y, size_x, size_y }
      nene:render_draw_rect(rectf:to_rect(), painter.use_lines, painter.color)

    case ShapeCategory.Circle then
      --tico.graphics_fill_circle(
      --  math.ifloor(global_pos.x),
      --  math.ifloor(global_pos.y),
      --  math.max(size_x, size_y) / 2,
      --  painter.color
      --)

    case ShapeCategory.Triangle then
      local p1, p2, p3 = shape.vertices[0], shape.vertices[1], shape.vertices[2]
      nene:render_draw_line(global_pos + p1, global_pos + p2, painter.color)
      nene:render_draw_line(global_pos + p2, global_pos + p3, painter.color)
      nene:render_draw_line(global_pos + p3, global_pos + p1, painter.color)

    case ShapeCategory.Polygon then
      for i = 0, < #shape.vertices - 1 do

        nene:render_draw_line(global_pos + shape.vertices[i],
                                    global_pos + shape.vertices[i+1],
                                    painter.color)
      end
      nene:render_draw_line(global_pos + shape.vertices[#shape.vertices-1],
                                  global_pos + shape.vertices[0],
                                  painter.color)
    end
  end
end

