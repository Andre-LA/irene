require 'irene.makers'

require 'irene.components.follower'
require 'irene.components.target'
require 'irene.components.position'
require 'irene.components.velocity'

local Comps, Sys = @Irene.Comps, @Irene.Sys

local FollowerSystem = @MakeSystem('FollowerSystem', @record{
  follower: Comps.Follower,
  target: Comps.Target,
  position: Comps.Position,
  velocity: Comps.Velocity,
})

global Sys.FollowerSystem = @FollowerSystem

function FollowerSystem:run(deltatime: number)
  ## if PROF then
    nprof:push(#[FollowerSystem.value.nickname]#)
    defer nprof:pop() end
  ## end

  for _, components in self:iterate_components() do
    local follower = components.follower
    local target   = components.target
    local position = components.position
    local velocity = components.velocity

    local dir_to_follow = target:get_direction(position:get_global_position())
    local vel_to_follow = dir_to_follow * follower.speed
    velocity:add_velocity(vel_to_follow * deltatime)
  end
end
