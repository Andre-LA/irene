require 'hashmap'

require 'nene'
require 'irene.core'
require 'irene.makers'
require 'nene.animation'

local Comps = @Irene.Comps

local AnimationPlayer = @record{
  current_animation: AnimationRange,
  animations: hashmap(string, AnimationRange),
  speed: number,
  last_change_time: uinteger,
  looping: boolean,
  paused: boolean,
}
## Irene.impl_is_component(AnimationPlayer)

global Comps.AnimationPlayer = @AnimationPlayer

function AnimationPlayer:change_animation(sprite: *Comps.Sprite, anim_name: string, current_time: uint32)
  local pos_animation_range = self.animations:peek(anim_name)

  if pos_animation_range ~= nilptr then
    if self.current_animation ~= pos_animation_range then
      self.current_animation = $pos_animation_range
      self.last_change_time = current_time
      sprite.current_frame = self.current_animation.from
    end
  else
    local msg = 'AnimationPlayer.change_animation: could not find animation to ' .. anim_name
    warn(msg)
    msg:destroy()
  end
end

function AnimationPlayer:destroy()
  self.animations:destroy()
end
